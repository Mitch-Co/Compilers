/*
  Created by: Fei Song
  File Name: tiny.cup
  To Build: java java_cup.Main < tiny.cup
*/
   
   
/* ----------------------Preliminary Declarations Section--------------------*/
   
/* Import the class java_cup.runtime.*  */
//import java_cup.runtime.*;
import absyn.*;
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    
    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */

    public boolean hasErrors = false;
    public void report_error(String message, Object info) {
   
        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        /* Add to the end of the StringBuffer error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }

:};
   
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
terminal IF, ELSE, WHILE, INT, VOID, RETURN;
terminal PLUS, MINUS, TIMES, OVER, LT, GT, LTEQ, GTEQ, NOTEQ;
terminal EQUALS, ASSIGN, SEMI, COMMA, LCURVED, RCURVED, LSQUARE, RSQUARE, LSQUIG, RSQUIG;
terminal String ID, NUM;
   
non terminal DecList program, declaration_list;
non terminal Dec declaration, fun_declaration;
non terminal VarDec var_declaration, param;
non terminal VarDecList local_decs_or, local_declarations, params_list, params;
non terminal ExpList arg_list, args, local_statements_or, statement_list;
non terminal Exp expression, factor, statement, expression_stmt, selection_stmt, iteration_stmt, return_stmt;
non terminal Var var;
non terminal NameTy type_specifier;
non terminal CompoundExp compound_stmt;
non terminal CallExp call;

precedence left ELSE,COMMA;
precedence nonassoc LT, GT, LTEQ, GTEQ, NOTEQ, EQUALS;
precedence left PLUS, MINUS;
precedence left TIMES, OVER;



/* ----------------------------Grammar Section-------------------- */
   
start with program;

program               ::= declaration_list:dl
                        {: 
                           RESULT = dl; 
                        :}
                        ;

declaration_list      ::= declaration_list:dl declaration:d
                        {: 
                           DecList t = dl;
                           while(t.tail != null)
                              t = t.tail;
                           t.tail = new DecList(d, null);
                           RESULT = dl;
                        :}
                        | declaration:d
                        {: 
                           RESULT = new DecList(d, null); 
                        :}
                        | error:e
                        {: 
                           report_fatal_error("SYNTAX ERROR CAUSING DEC LIST FAILURE", e);
                        :}
                        ;

declaration           ::= var_declaration:vd 
                        {: 
                           RESULT = vd; 
                        :}
                        | fun_declaration:fd 
                        {: 
                           RESULT = fd; 
                        :}
                        ;

var_declaration       ::= type_specifier:ts ID:i SEMI
                        {: 
                           RESULT = new SimpleDec(tsleft, tsright, ts, i); 
                        :}
                        | type_specifier:ts ID:i LSQUARE NUM:n RSQUARE SEMI
                        {: 
                           RESULT = new ArrayDec(tsleft, tsright, ts, i, new IntExp(nleft, nright, Integer.parseInt(n))); 
                        :}
                        ;

type_specifier        ::= INT:i 
                        {: 
                           RESULT = new NameTy(ileft, iright, NameTy._INT); 
                        :}
                        | VOID:v
                        {: 
                           RESULT = new NameTy(vleft, vright, NameTy._VOID); 
                        :}
                        ;

fun_declaration       ::= type_specifier:ts ID:i LCURVED params:p RCURVED compound_stmt:cs
                        {: 
                           RESULT = new FunctionDec(tsleft, tsright, ts, i, p, cs); 
                        :}
                        | error:e ID:i LCURVED params:p RCURVED compound_stmt:cs
                        {: 
                           report_error("FUNCTION DECLARATION ERROR", e);
                           hasErrors = true;
                           RESULT = new FunctionDec(eleft, eright, null , i, p, cs); 
                        :}
                        ;

params                ::= params_list:pl
                        {: 
                           RESULT = pl;
                        :}
                        | VOID:v
                        {: 
                           RESULT = new VarDecList(null, null); 
                        :}
                        ;

params_list           ::= params_list:pl COMMA param:p
                        {: 
                           VarDecList t = pl;
                           while(t.tail != null)
                              t = t.tail;
                           t.tail = new VarDecList(p, null);
                           RESULT = pl;
                        :}
                        | param:p
                        {: 
                           RESULT = new VarDecList(p, null); 
                        :}
                        ;

param                 ::= type_specifier:ts ID:i
                        {: 
                           SimpleDec toReturn = new SimpleDec(tsright, tsleft, ts, i);
                           toReturn.isParam = true;
                           RESULT = toReturn;
                        :}
                        | type_specifier:ts ID:i LSQUARE RSQUARE
                        {: 
                           ArrayDec toReturn = new ArrayDec(tsright, tsleft, ts, i, null); 
                           toReturn.isParam = true;
                           RESULT = toReturn;
                        :}
                        ;

compound_stmt         ::= LSQUIG local_decs_or:lds local_statements_or:sl RSQUIG
                        {: 
                           RESULT = new CompoundExp(ldsright, ldsleft, lds, sl); 
                        :}
                        ;

local_decs_or         ::= local_declarations:lds
                        {: 
                           RESULT = lds;
                        :}
                        |
                        {: 
                           RESULT = new VarDecList(null, null);
                        :}
                        ;

local_declarations    ::= local_declarations:lds var_declaration:vd
                        {: 
                           VarDecList t = lds;
                           while(t.tail != null)
                              t = t.tail;
                           t.tail = new VarDecList(vd, null);
                           RESULT = lds;
                        :}
                        | var_declaration:vd
                        {: 
                           RESULT = new VarDecList(vd, null); 
                        :}
                        ;

local_statements_or     ::= statement_list:sl
                        {: 
                           RESULT = sl;
                        :}
                        |
                        {: 
                           RESULT = new ExpList(null, null); 
                        :}
                        ;

statement_list        ::= statement_list:sl statement:s
                        {: 
                           ExpList t = sl;
                           while(t.tail != null)
                              t = t.tail;
                           t.tail = new ExpList(s, null);
                           RESULT = sl;
                        :}
                        | statement:s
                        {: 
                           RESULT = new ExpList(s, null); 
                        :}
                        ;

statement             ::= expression_stmt:es
                        {: 
                           RESULT = es; 
                        :}
                        | compound_stmt:cs
                        {: 
                           RESULT = cs; 
                        :}
                        | selection_stmt:ss
                        {: 
                           RESULT = ss;
                        :}
                        | iteration_stmt:is 
                        {: 
                           RESULT = is; 
                        :}
                        | return_stmt:rs
                        {: 
                           RESULT = rs;
                        :}
                        | error:e
                        {: 
                           report_error("STATEMENT ERROR", e);
                           hasErrors = true;
                           RESULT = new NilExp(eleft, eright);
                        :}
                        ;
expression_stmt       ::= expression:e SEMI
                        {: 
                           RESULT = e; 
                        :}
                        |SEMI:s
                        {: 
                           RESULT = new NilExp(sleft, sright); 
                        :}
                        ;

selection_stmt        ::= IF LCURVED expression:e RCURVED statement:s
                        {: 
                           RESULT = new IfExp(eleft, eright, e, s, null); 
                        :}
                        | IF LCURVED expression:e RCURVED statement:s ELSE statement:s2
                        {: 
                           RESULT = new IfExp(eleft, eright, e, s, s2);
                        :}
                        ;

iteration_stmt        ::= WHILE LCURVED expression:e RCURVED statement:s
                        {: 
                           RESULT = new WhileExp(eleft, eright, e, s); 
                        :}
                        ;

return_stmt           ::= RETURN expression:e SEMI
                        {: 
                           RESULT = new ReturnExp(eleft, eright, e); 
                        :}   
                        | RETURN SEMI:s
                        {: 
                           RESULT = new ReturnExp(sleft, sright, null); 
                        :}   
                        ;

expression           ::= var:v ASSIGN expression:e
                        {:
                           RESULT = new AssignExp(vleft, vright, v, e);
                        :}
                        | var:v ASSIGN error:e
                        {: 
                           report_error("ASSIGNMENT EXPRESSION ERROR", e);
                           hasErrors = true;
                           RESULT = new AssignExp(vleft, vright, v, null);
                        :}
                        | expression:e PLUS expression:e2
                        {: 
                           RESULT = new OpExp(eleft, eright, e, OpExp.PLUS, e2); 
                        :}
                        | expression:e MINUS expression:e2
                        {: 
                           RESULT = new OpExp(eleft, eright, e, OpExp.MINUS, e2); 
                        :}
                        | expression:e TIMES expression:e2
                        {: 
                           RESULT = new OpExp(eleft, eright, e, OpExp.MUL, e2); 
                        :}
                        | expression:e OVER expression:e2
                        {: 
                           RESULT = new OpExp(eleft, eright, e, OpExp.DIV, e2);  
                        :}
                        | expression:e LT expression:e2
                        {: 
                           RESULT = new OpExp(eleft, eright, e, OpExp.LT, e2);  
                        :}
                        | expression:e GT expression:e2
                        {: 
                           RESULT = new OpExp(eleft, eright, e, OpExp.GT, e2);  
                        :}
                        | expression:e LTEQ expression:e2
                        {: 
                           RESULT = new OpExp(eleft, eright, e, OpExp.LE, e2);  
                        :}
                        | expression:e GTEQ expression:e2
                        {: 
                           RESULT = new OpExp(eleft, eright, e, OpExp.GE, e2);  
                        :}
                        | expression:e NOTEQ expression:e2
                        {: 
                           RESULT = new OpExp(eleft, eright, e, OpExp.NE, e2);  
                        :}
                        | expression:e EQUALS expression:e2
                        {: 
                           RESULT = new OpExp(eleft, eright, e, OpExp.EQ, e2);  
                        :}
                        | factor:f
                        {:
                           RESULT = f;
                        :}
                        ;
                        
var                     ::= ID:i
                        {: 
                           RESULT = new SimpleVar(ileft, iright, i); 
                        :}
                        | ID:i LSQUARE expression:e RSQUARE
                        {: 
                           RESULT = new IndexVar(ileft, iright, i, e);
                        :}
                        ;

factor                  ::= LCURVED expression:e RCURVED
                        {: 
                           RESULT = e;
                        :}
                        | var:v
                        {: 
                           RESULT = new VarExp(vleft, vright, v);
                        :}
                        | call:c
                        {: 
                           RESULT = c;
                        :}
                        | NUM:n
                        {: 
                           RESULT = new IntExp(nleft, nright, Integer.parseInt(n)); 
                        :}
                        ;

call                  ::= ID:i LCURVED args:a RCURVED
                        {: 
                           RESULT = new CallExp(ileft, iright, i, a);
                        :}
                        ;

args                  ::= arg_list:el
                        {: 
                           RESULT = el; 
                        :}
                        |
                        {: 
                           RESULT = new ExpList(null, null); 
                        :}
                        ;

arg_list                ::= arg_list:el COMMA expression:e
                        {: 
                           ExpList tmp = el;
                           while(tmp.tail != null)
                              tmp = tmp.tail;
                           tmp.tail = new ExpList(e, null);
                           RESULT = el;
                        :}
                        | expression:e
                        {: 
                           RESULT = new ExpList(e, null); 
                        :}
                        ;
